ACTOR UsedMedipatch : Inventory {}

ACTOR MediPatch : CustomInventory replaces Stimpack
{
	Inventory.Icon "STIMICON"
	Inventory.InterHubAmount 0x7fffffff
	Inventory.MaxAmount 20
	Inventory.PickupMessage "Picked up a MediPatch."
	Tag "MediPatch Healing Agent"
	+INVENTORY.INVBAR
	States
	{
	Spawn:
		STIM A -1
		Stop
	Use:
		TNT1 A 0 A_JumpIf(health >= 100, "DontHeal2")
		TNT1 A 0 A_JumpIfInventory("UsedMedipatch", 1, "DontHeal")
		TNT1 A 0 A_GiveInventory("UsedMedipatch", 1)
		TNT1 A 0 ACS_NamedExecuteAlways("UseMedipatch", 0, 10, 3)
		Stop
	DontHeal:
		TNT1 A 0 A_Print("Can't use Medipatch, already healing!")
		Fail
	DontHeal2:
		TNT1 A 0 A_Print("Can't use Medipatch, health already full!")
		Fail
	}
}

ACTOR SuperMediPatch : CustomInventory replaces Medikit
{
	Inventory.Icon "MEDIICON"
	Inventory.InterHubAmount 0x7fffffff
	Inventory.MaxAmount 20
	Inventory.PickupMessage "Picked up a Super MediPatch."
	Tag "Super MediPatch Autosurgery Agent"
	+INVENTORY.INVBAR
	States
	{
	Spawn:
		MEDI A -1
		Stop
	Use:
		TNT1 A 0 A_JumpIf(health >= 100, "DontHeal2")
		TNT1 A 0 A_JumpIfInventory("UsedMedipatch", 1, "DontHeal")
		TNT1 A 0 A_GiveInventory("UsedMedipatch", 1)
		TNT1 A 0 ACS_NamedExecuteAlways("UseMedipatch", 0, 25, 5)
		Stop
	DontHeal:
		TNT1 A 0 A_Print("Can't use Medipatch, already healing!")
		Fail
	DontHeal2:
		TNT1 A 0 A_Print("Can't use Medipatch, health already full!")
		Fail
	}
}

ACTOR WaterFlask : CustomInventory replaces HealthBonus
{
	Inventory.Icon "FLSKICON"
	Inventory.InterHubAmount 0x7fffffff
	Inventory.MaxAmount 20
	Inventory.PickupMessage "Found some water."
	Tag "Water Bottle"
	Scale 0.65
	+INVENTORY.INVBAR
	+INVENTORY.UNDROPPABLE
	+INVENTORY.KEEPDEPLETED
	+INVENTORY.UNTOSSABLE
	States
	{
	Spawn:
		FLSK A -1
		Stop
	Use:
		TNT1 A 0 A_JumpIf(health >= 100, "DontDrinkNoNoise")
	UseLoop:
		TNT1 A 0 A_JumpIf(health >= 100, "DontDrink")
		TNT1 A 0 A_TakeInventory("WaterFlask", 1)
		TNT1 A 0 A_JumpIfInventory("AthleticSkillCounter", 80, 2)
		TNT1 A 0 A_GiveInventory("Health", 1)
		TNT1 A 0 A_JumpIfInventory("AthleticSkillCounter", 60, 2)
		TNT1 A 0 A_GiveInventory("Health", 1)
		TNT1 A 0 A_JumpIfInventory("AthleticSkillCounter", 40, 2)
		TNT1 A 0 A_GiveInventory("Health", 1)
		TNT1 A 0 A_JumpIfInventory("AthleticSkillCounter", 20, 2)
		TNT1 A 0 A_GiveInventory("Health", 1)
		TNT1 A 0 A_GiveInventory("Health", 1)
		TNT1 A 0 A_JumpIfInventory("WaterFlask", 1, "UseLoop")
	FlaskEmpty:
		TNT1 A 0 A_PlaySound("water/drink", CHAN_ITEM)
		Fail
	DontDrink:
		TNT1 A 0 A_PlaySound("water/drink", CHAN_ITEM)
		Fail
	DontDrinkNoNoise:
		TNT1 A 0 A_Print("Can't drink water, health already full!")
		Fail
	}
}