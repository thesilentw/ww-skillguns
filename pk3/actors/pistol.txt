ACTOR SkillClip : Ammo replaces Clip
{
  Inventory.PickupMessage "$GOTCLIP"
  Inventory.Amount 15
  Inventory.MaxAmount 30
  Ammo.BackpackAmount 15
  Ammo.BackpackMaxAmount 75
  Inventory.Icon "CLIPA0"
  States
  {
  Spawn:
    CLIP A -1
    Stop
  }
}

ACTOR Clip6 : SkillClip replaces Cell {}

ACTOR PistolLoaded : Ammo
{
	Inventory.MaxAmount 15
}

ACTOR SkillPistol : SkillWeapon
{
	Weapon.AmmoType1 "PistolLoaded"
	Weapon.AmmoUse1 1
	Weapon.AmmoGive1 0
	Weapon.AmmoType2 "SkillClip"
	Weapon.AmmoUse2 0
	Weapon.AmmoGive2 8
	Tag "Berducci XP4 Squall"
	+Weapon.NoAlert
	+Weapon.NoAutoFire
	+Weapon.Ammo_Optional
	States
	{
	Select:
		PISG A 1 Offset(0, -320)
		"####" A 1 A_SetTics(CallACS("GunSkillCheck") / 4)
		"####" A 1 Offset(-97, 115) A_JumpIfInventory("WeaponSkillActual", 50, 1)
		"####" A 1 Offset(-88, 105)
		"####" A 1 Offset(-74, 92) A_JumpIfInventory("WeaponSkillActual", 50, 1)
		"####" A 1 Offset(-56, 80)
		"####" A 1 Offset(-36, 69) A_JumpIfInventory("WeaponSkillActual", 50, 1)
		"####" A 1 Offset(-20, 59)
		"####" A 1 Offset(-11, 53) A_JumpIfInventory("WeaponSkillActual", 50, 1)
		"####" A 1 Offset(-6, 49)
		"####" A 1 Offset(-4, 45) A_JumpIfInventory("WeaponSkillActual", 50, 1)
		"####" A 1 Offset(-3, 43)
		"####" A 1 Offset(-3, 41) A_JumpIfInventory("WeaponSkillActual", 50, 1)
		"####" A 1 Offset(-2, 39)
		"####" A 1 Offset(-2, 37) A_JumpIfInventory("WeaponSkillActual", 50, 1)
		"####" A 1 Offset(-1, 35)
		"####" A 1 Offset(-1, 34) A_JumpIfInventory("WeaponSkillActual", 50, 1)
		"####" A 1 Offset(1, 33)
		"####" A 1 Offset(1, 32)
	SelectLoop:
		"####" A 1 A_Raise
		Loop
	Ready:
		PISG A 1 A_WeaponReady(WRF_ALLOWRELOAD)
		Loop
	AltfireReturn:
		PISG A 1 Offset(-72, 74)
		PISG A 1 Offset(-56, 61)
		PISG A 1 Offset(-42, 52)
		PISG A 1 Offset(-30, 45)
		PISG A 1 Offset(-20, 40)
		PISG A 1 Offset(-12, 37)
		PISG A 1 Offset(-6, 35)
		PISG A 1 Offset(-3, 34)
		PISG A 1 Offset(-1, 33)
		Goto Ready
	Fire:
		PISG A 0 A_PlaySound("pistol/ready", 6)
	FireLoop:
		SHTG A 0 A_JumpIfInventory("TriggerPullCounter", (CallACS("GunSkillCheck") / 5), "WeaponFire")
		SHTG A 0 A_JumpIfInventory("TriggerPullCounter", CallACS("GunSkillCheck"), "WeaponFire")
		SHTG A 0 A_JumpIfInventory("WeaponSkillActual", 100, "WeaponFire")
		PISG A 0 A_GiveInventory("TriggerPullCounter", 1)
		PISG A 0 ACS_NamedExecuteWithResult("GunJitter")
		PISG A 0 A_Jump(256, "RandomJitter1","RandomJitter2","RandomJitter3","RandomJitter4")
	RandomJitter1:
		PISG A 1 Offset(-1,32)
		PISG A 0 A_Refire("FireLoop")
		Goto ReleaseTrigger
	RandomJitter2:
		PISG A 1 Offset(1,32)
		PISG A 0 A_Refire("FireLoop")
		Goto ReleaseTrigger
	RandomJitter3:
		PISG A 1 Offset(-1,34)
		PISG A 0 A_Refire("FireLoop")
		Goto ReleaseTrigger
	RandomJitter4:
		PISG A 1 Offset(1,34)
		PISG A 0 A_Refire("FireLoop")
		Goto ReleaseTrigger
	ReleaseTrigger:
		PISG A 1 A_TakeInventory("TriggerPullCounter", 100)
		Goto Ready
	WeaponFire:
		PISG A 0 A_TakeInventory("TriggerPullCounter", 100)
		PISG A 0 A_JumpIfNoAmmo("Dryfire")
		PISG B 0 A_PlaySound("pistol/fire", CHAN_WEAPON)
		PISG B 0 A_GunFlash
		PISG B 1 BRIGHT Offset(0,40) A_FireBullets(0, 0, -1, 20, "SkillPuffPlayer", FBF_USEAMMO)
		PISG B 0 ACS_NamedExecuteWithResult("GunRecoil")
		PISG B 1 Offset(0,38) A_GiveInventory("WeaponSkillLevelupCounter", 1)
		PISG C 1 Offset(0,36) A_AlertMonsters
		PISG C 1 Offset(1,45)
		PISG C 1 Offset(1,55)
		PISG C 1 Offset(1,55)
		PISG A 1 Offset(1,53)
		PISG A 1 Offset(1,51)
		PISG A 1 Offset(0,49) A_WeaponReady(WRF_NOBOB)
		PISG A 1 Offset(0,46) A_WeaponReady(WRF_NOBOB)
		PISG A 1 Offset(0,43) A_WeaponReady(WRF_NOBOB)
		PISG A 1 Offset(0,40) A_WeaponReady(WRF_NOBOB)
		PISG A 1 Offset(0,37) A_WeaponReady(WRF_NOBOB)
		PISG A 1 Offset(0,35) A_WeaponReady(WRF_NOBOB)
		PISG A 1 Offset(0,34) A_WeaponReady(WRF_NOBOB)
		PISG A 1 Offset(0,33) A_WeaponReady(WRF_NOBOB)
		Goto Ready
	Dryfire:
		PISG A 1 Offset(0,35) A_PlaySound("Pistol/dryfire", 5)
		PISG A 1 Offset(0,34)
		PISG A 1 Offset(0,33)
		Goto Ready
	Reload:
		PISG A 0 A_JumpIfInventory("PistolLoaded", 15, 2)
		PISG A 0 A_JumpIfInventory("SkillClip", 1, "ReloadBegin")
		PISG A 1
		Goto Ready
	ReloadBegin:
		PISG A 1 Offset(0,34) A_PlaySound("Pistol/reloadstart",6)
		PISG A 1 Offset(0,36)
		PISG A 1 Offset(0,39)
		PISG A 1 Offset(0,41)
		PISG A 1 Offset(0,42)
		PISG A 1 Offset(0,41) A_PlaySound("Pistol/ClipOut",5)
		PISG A 1 Offset(0,39)
		PISG A 1 Offset(0,36)
		PISG A 1 Offset(1,33)
		PISG A 1 Offset(1,32)
		PISG A 1 Offset(1,31)
		PISR A 1 Offset(2,30)
		PISR A 1 Offset(4,28)
		PISR B 1 Offset(5,27)
		PISR B 1 Offset(4,29)
		PISR B 1 Offset(3,31)
		PISR B 1 Offset(1,32)
		PISR B 1 Offset(-1,33)
	ReloadSkillLoop:
		PISR B 1 A_SetTics(CallACS("GunSkillCheck") / 2)
	ReloadLoop:
		TNT1 A 0 A_TakeInventory("SkillClip",1)
		TNT1 A 0 A_GiveInventory("PistolLoaded",1)
		TNT1 A 0 A_JumpIfInventory("PistolLoaded",15,"ReloadFinish")
		TNT1 A 0 A_JumpIfInventory("SkillClip",1,"ReloadLoop")
		Goto ReloadFinish
	ReloadFinish:
		PISR B 1
		PISR B 1 A_Jump(CallACS("GunSkillCheck"), "ReloadFinish")
		PISR B 1 Offset(-2,35) A_PlaySound("Pistol/ClipIn",5)
		PISR B 1 Offset(-3,38)
		PISR B 1 Offset(-4,43)
		PISR B 1 Offset(-1,44)
		PISR A 1 Offset(2,43)
		PISR A 1 Offset(5,44)
		PISR A 1 Offset(8,45)
		PISG A 1 Offset(11,46)
		PISG A 1 Offset(12,48) A_SetTics(CallACS("GunSkillCheck") / 4)
		PISG A 1 Offset(12,50)
		PISG A 1 Offset(11,51)
		PISG A 1 Offset(10,50)
		PISG A 1 Offset(8,48)
		PISG A 1 Offset(6,45)
		PISG A 1 Offset(3,41)
		PISG A 1 Offset(2,35) A_PlaySound("Pistol/Cock",6)
		PISG A 1 Offset(1,33)
		PISG A 1 Offset(-1,32)
		Goto Ready
	Flash:
		TNT1 A 2 BRIGHT A_Light2
		TNT1 A 2 A_Light1
		TNT1 A 2 A_Light0
		Goto LightDone
	}
}